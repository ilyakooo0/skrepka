namespace Skrepka

open System

module Phonemic =

    // Urbit @p syllable tables (256 prefixes, 256 suffixes)
    // Each byte pair encodes as prefix[hi] + suffix[lo]

    let private prefixes =
        [| "doz"; "mar"; "bin"; "wan"; "sam"; "lit"; "sig"; "hid"
           "fid"; "lis"; "sog"; "dir"; "wac"; "sab"; "wis"; "sib"
           "rig"; "sol"; "dop"; "mod"; "fog"; "lid"; "hop"; "dar"
           "dor"; "lor"; "hod"; "fol"; "rin"; "tog"; "sil"; "mir"
           "hol"; "pas"; "lac"; "rov"; "liv"; "dal"; "sat"; "lib"
           "tab"; "han"; "tic"; "pid"; "tor"; "bol"; "fos"; "dot"
           "los"; "dil"; "for"; "pil"; "ram"; "tir"; "win"; "tad"
           "bic"; "dif"; "roc"; "wid"; "bis"; "das"; "mid"; "lop"
           "ril"; "nar"; "dap"; "mol"; "san"; "loc"; "nov"; "sit"
           "nid"; "tip"; "sic"; "rop"; "wit"; "nat"; "pan"; "min"
           "rit"; "pod"; "mot"; "tam"; "tol"; "sav"; "pos"; "nap"
           "nop"; "som"; "fin"; "fon"; "ban"; "mor"; "wor"; "sip"
           "ron"; "nor"; "bot"; "wic"; "soc"; "wat"; "dol"; "mag"
           "pic"; "dav"; "bid"; "bal"; "tim"; "tas"; "mal"; "lig"
           "siv"; "tag"; "pad"; "sal"; "div"; "dac"; "tan"; "sid"
           "fab"; "tar"; "mon"; "ran"; "nis"; "wol"; "mis"; "pal"
           "las"; "dis"; "map"; "rab"; "tob"; "rol"; "lat"; "lon"
           "nod"; "nav"; "fig"; "nom"; "nib"; "pag"; "sop"; "ral"
           "bil"; "had"; "doc"; "rid"; "moc"; "pac"; "rav"; "rip"
           "fal"; "tod"; "til"; "tin"; "hap"; "mic"; "fan"; "pat"
           "tac"; "lab"; "mog"; "sim"; "son"; "pin"; "lom"; "ric"
           "tap"; "fir"; "has"; "bos"; "bat"; "poc"; "hac"; "tid"
           "hav"; "sap"; "lin"; "dib"; "hos"; "dab"; "bit"; "bar"
           "rac"; "par"; "lod"; "dos"; "bor"; "toc"; "hil"; "mac"
           "tom"; "dig"; "fil"; "fas"; "mit"; "hob"; "har"; "mig"
           "hin"; "rad"; "mas"; "hal"; "rag"; "lag"; "fad"; "top"
           "mop"; "hab"; "nil"; "nos"; "mil"; "fop"; "fam"; "dat"
           "nol"; "din"; "hat"; "nac"; "ris"; "fot"; "rib"; "hoc"
           "nim"; "lar"; "fit"; "wal"; "rap"; "sar"; "nal"; "mos"
           "lan"; "don"; "dan"; "lad"; "dov"; "riv"; "bac"; "pol"
           "lap"; "tal"; "pit"; "nam"; "bon"; "ros"; "ton"; "fod"
           "pon"; "sov"; "noc"; "sor"; "lav"; "mat"; "mip"; "fip" |]

    let private suffixes =
        [| "zod"; "nec"; "bud"; "wes"; "sev"; "per"; "sut"; "let"
           "ful"; "pen"; "syt"; "dur"; "wep"; "ser"; "wyl"; "sun"
           "ryp"; "syx"; "dyr"; "nup"; "heb"; "peg"; "lup"; "dep"
           "dys"; "put"; "lug"; "hec"; "ryt"; "tyv"; "syd"; "nex"
           "lun"; "mep"; "lut"; "sep"; "pes"; "del"; "sul"; "ped"
           "tem"; "led"; "tul"; "met"; "wen"; "byn"; "hex"; "feb"
           "pyl"; "dul"; "het"; "mev"; "rut"; "tyl"; "wyd"; "tep"
           "bes"; "dex"; "sef"; "wyc"; "bur"; "der"; "nep"; "pur"
           "rys"; "reb"; "den"; "nut"; "sub"; "pet"; "rul"; "syn"
           "reg"; "tyd"; "sup"; "sem"; "wyn"; "rec"; "meg"; "net"
           "sec"; "mul"; "nym"; "tev"; "web"; "sum"; "mut"; "nyx"
           "rex"; "teb"; "fus"; "hep"; "ben"; "mus"; "wyx"; "sym"
           "sel"; "ruc"; "dec"; "wex"; "syr"; "wet"; "dyl"; "myn"
           "mes"; "det"; "bet"; "bel"; "tux"; "tug"; "myr"; "pel"
           "syp"; "ter"; "meb"; "set"; "dut"; "deg"; "tex"; "sur"
           "fel"; "tud"; "nux"; "rux"; "ren"; "wyt"; "nub"; "med"
           "lyt"; "dus"; "neb"; "rum"; "tyn"; "seg"; "lyx"; "pun"
           "res"; "red"; "fun"; "rev"; "ref"; "mec"; "ted"; "rus"
           "bex"; "leb"; "dux"; "ryn"; "num"; "pyx"; "ryg"; "ryx"
           "fep"; "tyr"; "tus"; "tyc"; "leg"; "nem"; "fer"; "mer"
           "ten"; "lus"; "nus"; "syl"; "tec"; "mex"; "pub"; "rym"
           "tuc"; "fyl"; "lep"; "deb"; "ber"; "mug"; "hut"; "tun"
           "byl"; "sud"; "pem"; "dev"; "lur"; "def"; "bus"; "bep"
           "run"; "mel"; "pex"; "dyt"; "byt"; "typ"; "lev"; "myl"
           "wed"; "duc"; "fur"; "fex"; "nul"; "luc"; "len"; "ner"
           "lex"; "rup"; "ned"; "lec"; "ryd"; "lyd"; "fen"; "wel"
           "nyd"; "hus"; "rel"; "rud"; "nes"; "hes"; "fet"; "des"
           "ret"; "dun"; "ler"; "nyr"; "seb"; "hul"; "ryl"; "lud"
           "rem"; "lys"; "fyn"; "wer"; "ryc"; "sug"; "nys"; "nyl"
           "lyn"; "dyn"; "dem"; "lux"; "fed"; "sed"; "bec"; "mun"
           "lyr"; "tes"; "mud"; "nyt"; "byr"; "sen"; "weg"; "fyr"
           "mur"; "tel"; "rep"; "teg"; "pec"; "nel"; "nev"; "fes" |]

    let private prefixMap =
        prefixes |> Array.mapi (fun i s -> s, byte i) |> Map.ofArray

    let private suffixMap =
        suffixes |> Array.mapi (fun i s -> s, byte i) |> Map.ofArray

    let toOb (bytes: byte[]) : string =
        bytes
        |> Array.chunkBySize 2
        |> Array.map (fun pair -> $"{prefixes.[int pair.[0]]}{suffixes.[int pair.[1]]}")
        |> String.concat "-"

    type ObValidation =
        | Empty
        | Partial of validPairs: int
        | InvalidSyllable of syllable: string
        | Valid

    let validateOb (s: string) : ObValidation =
        let s = s.Trim()

        if s = "" then
            Empty
        else
            let parts = s.Split('-', StringSplitOptions.RemoveEmptyEntries)

            // Don't flag the trailing part if still being typed
            let completeParts =
                if parts.Length > 0 && parts.[parts.Length - 1].Length < 6 then
                    parts.[.. parts.Length - 2]
                else
                    parts

            let firstInvalid =
                completeParts
                |> Array.tryFind (fun sp ->
                    sp.Length <> 6
                    || not (Map.containsKey sp.[0..2] prefixMap)
                    || not (Map.containsKey sp.[3..5] suffixMap))

            match firstInvalid with
            | Some s -> InvalidSyllable s
            | None when completeParts.Length = 16 -> Valid
            | None -> Partial completeParts.Length

    let fromOb (s: string) : byte[] option =
        let pairs = s.Split('-', StringSplitOptions.RemoveEmptyEntries)

        if pairs.Length = 0 then
            None
        else

            let decoded =
                pairs
                |> Array.choose (fun sp ->
                    if sp.Length = 6 then
                        match Map.tryFind sp.[0..2] prefixMap, Map.tryFind sp.[3..5] suffixMap with
                        | Some hi, Some lo -> Some(hi, lo)
                        | _ -> None
                    else
                        None)

            if decoded.Length = pairs.Length then
                decoded |> Array.collect (fun (hi, lo) -> [| hi; lo |]) |> Some
            else
                None
